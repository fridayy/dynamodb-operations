plugins {
    id "org.sonarqube" version "2.6"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    jacocoTestReport {
        additionalSourceDirs = files(sourceSets.main.allSource.srcDirs)
        sourceDirectories = files(sourceSets.main.allSource.srcDirs)
        classDirectories =  files(sourceSets.main.output)
        executionData = project.fileTree(dir: "${project.buildDir}/jacoco", include: "**/*.exec")
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }
}

subprojects {
    apply plugin: 'groovy'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    if (it.name != "dynamodb-common") {
        dependencies {
            compile project(':dynamodb-common')
            compile 'com.amazonaws:aws-java-sdk-dynamodb:1.11.419'
            compile 'org.slf4j:slf4j-api:1.8.0-beta2'
            compile 'org.slf4j:slf4j-simple:1.8.0-beta2'
        }
    }

    dependencies {
        compile 'org.jetbrains:annotations:16.0.3'
        testCompile 'org.spockframework:spock-core:1.2-groovy-2.5'
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPaths", "$buildDir/jacoco/aggregated.exec"
    }
}



task mergeJacoco(type: JacocoMerge) {
    destinationFile = file("$buildDir/jacoco/aggregated.exec")
    executionData = project.fileTree(dir: ".", include: "**/build/jacoco/test.exec")
}

mergeJacoco.dependsOn jacocoTestReport